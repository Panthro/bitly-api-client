
plugins {
	id 'java'
	id 'jacoco'
	id 'maven-publish'
	id 'project-report'
	id 'com.jfrog.bintray' version '1.8.4'
}

description = 'Bitly API Client'
group       = 'net.swisstech'
version     = '0.8.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

/*
 * SOURCE SETS
 */
sourceSets {
	intTest {
		compileClasspath = sourceSets.main.output + configurations.testRuntime
		runtimeClasspath = sourceSets.main.output + configurations.testRuntime + output
	}
}

/*
 * REPOSITORIES
 */
repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

/*
 * DEPENDENCIES
 */
ext.versions = [
		  slf4j    : '1.8.0-beta2'
		, testng   : '6.14.3'
		, mockito  : '1.10.19'
		, jodatime : '2.10'
		, gson     : '2.8.5'
		, logger   : '2.0.0'
]

dependencies {
	compile        "com.google.code.gson:gson:${versions.gson}"
	compile        "joda-time:joda-time:${versions.jodatime}"
	compile        "org.slf4j:slf4j-api:${versions.slf4j}"

	testCompile    "org.slf4j:slf4j-simple:${versions.slf4j}"
	testCompile    "org.testng:testng:${versions.testng}"
	testCompile    "org.mockito:mockito-all:${versions.mockito}"
	testCompile    "net.swisstech:logger:${versions.logger}"

	intTestCompile "org.slf4j:slf4j-simple:${versions.slf4j}"
	intTestCompile "org.testng:testng:${versions.testng}"
	intTestCompile "org.mockito:mockito-all:${versions.mockito}"
	intTestCompile "net.swisstech:logger:${versions.logger}"
}

/*
 * UNIT TESTING
 */
jacocoTestReport {
	reports.html.destination = file("${reportsDir}/jacoco/${name}")
}

test {
	useTestNG()
	testLogging.showStandardStreams = true

	jacoco {
		destinationFile = file("${buildDir}/jacoco.ut.exec")
	}
}

/*
 * INTEGRATION TESTING
 */
configurations {
	intTestCompile
}

task('intTest', type: Test, dependsOn: 'intTestClasses') {
	useTestNG()
	testLogging.showStandardStreams = true
	group           = 'verification'
	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath       = sourceSets.intTest.runtimeClasspath

	jacoco {
		destinationFile = file("${buildDir}/jacoco.it.exec")
	}
}

task('jacocoIntTestReport', type: JacocoReport) {
	reports.html.destination = file("${reportsDir}/jacoco/${name}")
	executionData intTest
	sourceSets sourceSets.main
}

/*
 * WRAPPER
 */
wrapper {
	gradleVersion = '4.10'
}

/*
 * PACKAGING
 */
task sourceJar(type: Jar) {
	from sourceSets.main.allSource
}

/*
 * PUBLISHING
 */
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourceJar {
				classifier 'sources'
			}
		}
	}
}

bintray {
	// key and user must be in your ~/.gradle/gradle.properties
	user         = project.hasProperty('swisstech_bintray_user')   ? project.swisstech_bintray_user   : System.getenv('SWISSTECH_BINTRAY_USER')
	key          = project.hasProperty('swisstech_bintray_apikey') ? project.swisstech_bintray_apikey : System.getenv('SWISSTECH_BINTRAY_KEY')
	dryRun       = false
	publish      = true
	publications = [ 'mavenJava' ]
	pkg {
		repo            = 'maven'
		name            = project.name
		desc            = project.description
		licenses        = [ 'Apache-2.0' ]
		websiteUrl      = "https://github.com/stackmagic/${project.name}"
		vcsUrl          = "https://github.com/stackmagic/${project.name}.git"
		issueTrackerUrl = "https://github.com/stackmagic/${project.name}/issues"
		publicDownloadNumbers = true
		version {
			name = project.version
			desc = project.description
		}
	}
}
